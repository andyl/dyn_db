#!/usr/bin/env elixir 

Mix.install([
  {:ecto, "~> 3.9"},
  {:ecto_sql, "~> 3.9"},
  {:ecto_sqlite3, "~> 0.9"},
])

Application.put_env(:foo, Repo, database: "ecto_ql_example.db")

defmodule Repo do
  use Ecto.Repo,
    adapter: Ecto.Adapters.SQLite3, 
    otp_app: :foo
end

defmodule Migration0 do
  use Ecto.Migration

  def change do
    create table("posts") do
      add(:title, :string)
      timestamps(type: :utc_datetime_usec)
    end
  end
end

defmodule Post do
  use Ecto.Schema

  schema "posts" do
    field(:title, :string)
    timestamps(type: :utc_datetime_usec)
  end
end

defmodule Main do
  import Ecto.Query

  def main do
    children = [
      Repo
    ]

    _ = Repo.__adapter__().storage_down(Repo.config())
    :ok = Repo.__adapter__().storage_up(Repo.config())

    {:ok, _} = Supervisor.start_link(children, strategy: :one_for_one)

    Ecto.Migrator.run(Repo, [{0, Migration0}], :up, all: true, log_migrations_sql: :debug)
    Repo.insert!(%Post{title: "Hello, World!"})

    from(Post)
    |> Repo.all()
    |> IO.inspect()
  end
end

Main.main()
